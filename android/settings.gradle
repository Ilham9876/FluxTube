pluginManagement {
    def flutterSdkPath = {
        def properties = new Properties()
        file("local.properties").withInputStream { properties.load(it) }
        def flutterSdkPath = properties.getProperty("flutter.sdk")
        assert flutterSdkPath != null, "flutter.sdk not set in local.properties"
        return flutterSdkPath
    }
    settings.ext.flutterSdkPath = flutterSdkPath()

    includeBuild("${settings.ext.flutterSdkPath}/packages/flutter_tools/gradle")

    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }
}

plugins {
    id "dev.flutter.flutter-plugin-loader" version "1.0.0"
    id "com.android.application" version '8.7.3' apply false
    id "org.jetbrains.kotlin.android" version "2.1.0" apply false
}

include ":app"

// Fix for plugins that don't declare namespace (required for AGP 8+)
gradle.beforeProject { project ->
    project.afterEvaluate {
        if (project.plugins.hasPlugin('com.android.library')) {
            if (project.android.namespace == null) {
                def manifest = project.file("src/main/AndroidManifest.xml")
                if (manifest.exists()) {
                    def packageName = new groovy.xml.XmlSlurper().parse(manifest).@package.toString()
                    if (packageName) {
                        project.android.namespace = packageName
                    }
                }
            }
        }
    }
}
